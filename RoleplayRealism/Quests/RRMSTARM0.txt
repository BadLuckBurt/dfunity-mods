Quest: RRMSTARM0
DisplayName: Mountain Rumors
QRC:

QuestorOffer:  [1000]
<ce> Rumors have reached us from some of our guild colleagues in  
<ce> the mountains concerning a famed master of armor crafting.
<ce>
<ce> It seems that %g just simply vanished a while back and no 
<ce> one has heard anything of %g2 since. Recently some reports 
<ce> have been received from over in the ____guild_ 
<ce> regarding some noble whose soldiers and personal guard  
<ce> have been seen clad in some rather impressive armor by 
<ce> several locals. The guild smiths think that the descriptions 
<ce> sound very similar in style to, and could very well be, the  
<ce> work of this missing master armor-smith.
<ce> 
<ce> We suspect that there's something rather fishy going on 
<ce> here, and our opinion is that it requires attention from
<ce> a guild master.
<ce>
<ce> Would you be willing to head over there to investigate and
<ce> deal with the situation, before a guild member with less 
<ce> skill and experience makes a mess of things?

RefuseQuest:  [1001]
<ce> Okay, I understand that as a guild master you're too 
<ce> busy to drop everything to investigate every rumor that
<ce> surfaces. Of course we all respect that, however I really
<ce> think this one is worth the attention of a guild master, 
<ce> otherwise I wouldn't have mentioned it to you. Hopefully 
<ce> one of the other guild masters will be willing to help out.

AcceptQuest:  [1002]
<ce> Great, thank you Master %pcl. We're convinced  
<ce> that this is worth further investigation. Please head  
<ce> over to the guild hall at __guild_ in the
<ce> ____guild_ to see what you can discover.
<ce> 
<ce> There will be a contact waiting for you there who can
<ce> bring you up to speed regarding the situation. They
<ce> will wait for your arrival for six months, after that
<ce> the local members will likely try to deal with the  
<ce> situation themselves, and probably bungle it which 
<ce> I think you'd agree is best avoided.
<ce>
<ce> Hopefully this will lead you to this lost master armorer.

QuestFail:  [1003]
<ce> The guild is disappointed that you were unable to find the time to
<ce> investigate what was going on over in the ____guild_.
<ce>
<ce> Hopefully it was nothing unsavory, and the missing armorer is
<ce> simply living out his days quietly somewhere.

QuestLogEntry: [1010]
On %qdt:
 _qgiver_ of the Fighters Guild in ___qgiver_
 informed me about some rumors regarding a master 
 armorer who's not been seen for rather a long time.
    I've agreed to investigate and need to travel  
 to __guild_ in the ____guild_ to see if 
 I can find out what is going on and whether or not 
 it's actually connected with %g3 disappearance.

Message: 1011
On %qdt:
 You were able to make time to head over to the guild
 in __guild_ of the ____guild_ to investigate.
 Hopefully it will lead to the missing armorer.

Message: 1012
On %qdt:
 You were unable to find time to investigate what was 
 going on over in the ____guild_. Hopefully it was 
 nothing untoward, and the missing armorer is simply
 living out his days somewhere peacefully.

Message: 1020
<ce> Do you think you'll be able to go to __guild_ and investigate
<ce> the rumors from ____guild_ soon, Master %pct?

Message: 1021
<ce> You have arrived at __guild_ where the rumors regarding 
<ce> the lost armorer surfaced. You should head over to the
<ce> local guild hall to meet the contact who's waiting for
<ce> your arrival.

QBN:

-- Town west of Northrock_Fort 
Place _guild_ permanent Aldleigh

Person _qgiver_ group Questor

Clock _investigate_ 182.00:00 

-- Quest start-up:
    log 1010 step 0
    start timer _investigate_
    pc at _guild_ do _beginInvestigation_ saying 1021

_beginInvestigation_ task:
    start quest RRMSTARM1
    stop timer _investigate_
    log 1011 step 1
    give pc nothing
    end quest

_investigate_ task:
    log 1012 step 1
    end quest saying 1003

-- Return to questgiver
_qgclicked_ task:
    clicked npc _qgiver_ say 1020
    clear _qgclicked_
