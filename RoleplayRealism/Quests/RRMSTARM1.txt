Quest: RRMSTARM1
DisplayName: The Lost Master Armorer
QRC:

QuestFail:  [1003]
__fort_ is no longer on alert for you, and have locked the doors.
You have failed to rescue _armorer_ from his predicament in the
____fort_.

QuestComplete:  [1004]
<ce>Excellent work Master, you were able rescue 
_armorer_ from __fort_ out here in the
____fort_. I feel much better now that the
sitution has been resolved so well.
It's been an absolute privilege to watch a 
Master of the guild at work, %pcf. Please
do pop in and say hello if you're ever up in
the mountains again wont you?

RumorsDuringQuest:  [1005]
<ce> The guards over at the __fort_
<ce> in ____fort_ have such fancy 
<ce> armor. I wonder where they get such 
<ce> workmanship?

RumorsPostsuccess:  [1007]
<ce> You're that %ra that rescued the master
<ce> armorer _armorer_ from 
<ce> __fort_. Bloody nobility, think
<ce> they can treat commoners like dirt and
<ce> get away with it. Praise be for champions
<ce> like you who help the common folk.

QuestLogEntry: [1010]
On %qdt:
 I met with _contact_ of the Fighters Guild
 in __guild_ who told me about the rumors regarding 
 a master armorer called _armorer_ who has not 
 been seen for a long time.
 I'm investigating to see if I can find out where these
 troops with fine armor are from, what's going on, and 
 whether or not it's connected with %g3 disapearance.

Message: 1011
on %qdt:
 I have found the armorer, _armorer_. He is
 trapped at __fort_ in servitude to a noble called
 _lord_. He and his troops seem to be nothing
 more than brigands and renegades, and you suspect this
 lord is probably the discraced or iligitimate son of
 some noble family somewhere. 
 They are keeping the smith from leaving by threatening
 his daughter by way of a contract with the Dark
 Brotherhood. Fighting the entire garrison at the fort 
 is unlikely to be possible so I need to get hold of 
 this contract to free _armorer_ from the villanous 
 clutches of _lord_. Alternatively, he has agreed to 
 let the smith go if I pay the ridiculous sum of a 
 million gold.

Message: 1012
on %qdt:
 My guild contact in __guild_, _contact_, 
 has arranged a meeting for me with the Dark Brotherhood
 over in __dbTavern_.
 They will wait for you in the tavern called
 _dbTavern_. Look for someone called 
 _dbContact_.

Message: 1013
on %qdt:
 At a meeting with a representative of the Dark
 Brotherhood arranged by _contact_,
 I discovered that the contract is actually 
 unsanctioned and off the books. The brotherhood
 tend to be un-interested in death contracts on
 peasant wormen for the purpose of blackmailing
 a commoner of no real consequence, however 
 skilled he may be at making armor.
 Due to my good standing with the Dark Brotherhood,
 they are going to deal with this renegade brother
 swiftly, retrieving the contract and delivering it
 to me within the week. Whoever this transgressing
 member is, they're about to regret ever having
 taken money from _lord_ against the code 
 of the brotherhood!

Message: 1014
on %qdt:
 At a meeting with a representative of the Dark
 Brotherhood arranged by _contact_,
 I discovered that the contract is actually 
 unsanctioned and off the books. The brotherhood
 tend to be un-interested in death contracts on
 peasant wormen for the purpose of blackmailing
 a commoner of no real consequence, however 
 skilled he may be at making armor. 
 The brotherhood met with me out of respect for
 my rank in the Fighters Guild, but they are not
 going to deal with this transgressing brother
 until they're ready to, which is not quick enough. 
 However, I have been permitted to retrieve
 the contract myself on the understanding that I
 do it quietly without hurting any other members
 in the process. If I do, there will be costly
 consequences.
 I was told that I should be able to find the 
 renegade member at __dbHideout_ 
 in the _dbHideout_.

Message: 1015
on %qdt:
 Thankfully I was able to release _armorer_ from
 his entrapment by _lord_ at _fort_.
 He has headed south to a small town called Pjiga in
 the Mournoth region to set up a new forge and teach
 an apprentice.

Message: 1016
on %qdt:
 Thankfully I was able to release _armorer_ from
 his entrapment by _lord_ at __fort_.
 He has headed west to a small town called Penmore
 in Glenpoint to set up a new forge and teach
 an apprentice.

Message: 1017
on %qdt:
 Thankfully I was able to release _armorer_ from
 his entrapment by _lord_ at __fort_.
 He has headed south west to a small town called 
 Paponirea in Tigonus to set up a new forge and
 teach an apprentice.


Message: 1020
<ce>[A guard to your left speaks to you as you enter..]
<ce>
<ce>Hey, who are you? What are you doing here and what do
<ce>you want %ra? Yes, over here on your left, are you deaf?
<ce>
<ce>You had better go and speak to _captain_ immediately!
<ce> 
<ce>Continue further down the hall this way, then go to the 
<ce>right, in the big room. Keep your weapons sheathed and
<ce>don't try any funny stuff, or we'll have your guts for
<ce>garters.

Message: 1021
<ce>[A guard behind you sees you speaking and shouts..]
<ce>
<ce>Hey, what are you doing in there You've made a big 
<ce>mistake, we don't take kindly to trespassing.
<ce>
<ce>You'Ll not leave here alive %ra...

Message: 1022
<ce>[As you slice through yet another of their number, the lord shouts..]
<ce>
Holy gods above, this bloody %ra's a Daedra in human form or
something...  EVERYONE STOP!
<ce>
We surrender %pcn. Take _armorer_ and just leave, he's not 
worth losing this many people for, no matter how good the 
armor he makes is.

Message: 1023
Argghhh. You're going to pay in blood for that, %ra!

Message: 1024
__fort_ is no longer on alert for you, and have locked the doors.
You have failed to rescue _armorer_ from his predicament in the
____fort_.

Message: 1025
The warriors of __fort_ and their leader are enraged and will
be searching for you for the next couple of days

Message: 1026
You've arrived at __fort_ out here in the hills way off the
beaten track and miles away from anywhere else. They must 
have re-built the place from a semi-ruin, as you can see 
signs of repairs everywhere. There are also indications that
a large group are living at the fort, and some significant
building work has taken place. You suspect the fort has had
it's cellars converted to barracks. This place is likely to
be more dangerous than it might appear at first glance.

-- Lord
Message: 1030
You dare to speak to me without permission? 
Guards, chase this idiot out of here!

Message: 1031
So, you're here from the Fighters Guild %pcf? I'm sure my
captain has told you to behave yourself, see that you do so
else my guards get more training.
Does the the guild need help from us with something?
I might consider it for a large fee.
Come on, what do you want? Out with it!

Message: 1032
Who makes our armor? Haha. Well, who made your armour, %ra?
That's a bit of an impertinent question, but there's a simple
answer: we have our own armorer, of course.
Does that surprise you, that we have such a fine smith all
the way out here in the middle of nowhere? You city types
are so arrogant. No, he doesn't want to work for anyone
else, and I wont allow him to freelance. You're out of luck.

Message: 1033
Stop bothering me if you have nothing new to say.

Message: 1034
And what you gonna do bout it, %ra?
I'll release _armorer_ for, say, a million gold. How about 
that, haha.
Can you afford him? Let's see if you can bring that much gold.

Message: 1035
Nice, I'm surprised you could get so much gold.

Message: 1036
WHat! How the hell did you get hold of that. Crap!

Message: 1037
Okay you can take _armorer_ with you, we're going to miss his
craftmanship but I know when the game is up. 
I suggest that you don't ever cross our path again, %pcf.


-- Captain
Message: 1040
Who are you? What business do you have with us, %ra?
You say you're a high ranking member of the Fighters Guild?
Well we have no issues with the guild so let's keep
it that way, eh? 
You may wander our fort freely and
speak with our lord. But, %pcf, don't cause any trouble
or do anything stupid as we wont hesitate to cut you down,
representative of the Fighters Guild or not.

Message: 1041
Where do we get such fine quality armor? Well you're a nosey 
%ra aren't you? I'm not going to answer that, ask our lord, 
but I suggest you tread carefully.

Message: 1042
You want _armorer_? No!

Message:  1043
Just take _armorer_ and leave us be. 
I don't ever want to see you again.


-- Armorer
Message: 1050
Who the hell are you? Go away, or you'll get us both killed.
Heavens above leave damn you, before the guards see you.

Message: 1051
<ce>[He glances nervously over your shoulder as you speak..]
<ce>
Who the hell are you? Go away, or you'll get me into trouble.
I don't want to be seen talking to you.

Message: 1052
I overheard a guard saying that you were asking the lord about
me. Franky, I'm amazed you're still alive, and even more surprised
you can wander freely about the place. Seems like it might be safe
to talk to you after all.
I'm _armorer_ and I'm trapped here, %pcf. Been here for nearly
two years now, making armor for these damned marauders. Oh, don't
let them fool you, their 'lord' is no noble. No, he's led his
band of renegades for years and they cleared out and setup camp
in this fort a few years back. They use it as a base, since it's 
pretty isolated up here in the mountains, they can operate from
here with relative impunity.

Message: 1053
How did I end up trapped here you ask? Gambling and drinking.
After my wife died and my daughter married, I decided to travel
some while I still could. So I sold my forge, hung up my tools
and travelled around for several months before ending up in
these mountains. One night I was too deep in my cups, as I was
missing my wife something fierce, and started gambling with
some local lord. Seemed more like a ruffian, but he had money.
Ended up with quite the debt to this 'lord' and we came to an
arrangement where I would work crafting armor for him and his men
to pay off the debt. 
Three months, it was only supposed to be for
three months. When the time was up and I started preparing to
leave, they surrounded me and told me I was goin nowhere.
I knew I couldn't fight my way out alone, so started to make
plans to sneak out and escape, but the captain came and made
it very clear that if I were to go missing, that my daughter
would die. This cursed 'lord' has paid the Dark Brotherhood
for a contract on her, to be carried out on his word if I go
missing. That damnable cutthroat son of a bitch knew he had
me, and I've been here ever since. 
She's all I have left now since my wife died, and lives with
her husband farming peacfully. I'll never let her come to harm
which means I am trapped here in servitude until I die unless
some miracle occurs.

Message: 1058
My god %ra, you're crazy! You'll never beat a whole garrison
by yourself. Dibella have mercy on your soul.

Message: 1059
Great, thanks %pcf. Let's get the hell out of here!


-- Guild Contact
Message: 1060
Welcome to __guild_ master %pcn. My name is
_contact_ and I'm very happy to see you indeed, 
as the situation is a rather perplexing one to 
say the least.
<ce>
We've been hearing rumors of a local noble whose soldiers 
and personal guard have been seen clad in unusually fine 
armor. Thing is no one knows anything much about
this 'noble', where he resides or what family they're
from. I've been looking into the matter for a while now,
and have found three reliable locals who've seen them
firsthand.

Message: 1061
I've taken the liberty of making inquiries with a local
group of trackers about tracking down these soldiers and
their mysterious leader. Unfortunately expertise like 
this doesn't come cheap however, and it will cost 7000 gold 
to engage their services. 
<ce>
Alternatively, you could try to search for the location
yourself. I can tell you what the three reliable eye-witnesses 
told me which is all we have to go on.
<ce>
So would you like me to employ the trackers? I'll obviously
need you to provide the 7000 gold to cover their price.

-arrange for you to meet the three 
-witnesses and listen to their accounts firsthand.

Message: 1062
Alright then, it is a large sum so I understand not wanting
to spend that much. Here you go:
1) East of __guild_
2) South of Hawkwing Orchard
3) North East of the Masterhart Graveyard
Good luck and let me know if you need any help once you
find them and hopefully a bit more about the situation.
Tread carefully, I doubt that they'll be very forgiving if 
you anger them.

Message: 1063
Alright then, I'll get in touch with them and check they
are not busy with another job. Please return with the 
gold as soon as you can, if you want to proceed.

Message: 1064
Thanks for bringing the gold, %pcf. Good news, the trackers
have agreed to take on this task for us and I've paid them 
1000 gold to cover expenses to get started immediately.
<ce>
They'll get the remainder once they've discovered the 
location for us. They reckon it will take a week, so
please rest and prepare in the meantime. I'll send a
letter upon their return.

Message:  1065
<ce>             You're approached by a young boy who smiles in
<ce>             recognition and does't ask your name before handing
<ce>             you a letter, then swiftly runs off into the town.
                                     <--->
<ce>             A letter is pressed into your hand. You spin
<ce>            to see who gave it to you. You catch a glimpse
<ce>                of livery that vanishes into the crowd.
                                     <--->
<ce>              A girl steps forward saying "Letter for you
<ce>                     %pcn," and hands you a note.

Message: 1066
%pcn,
 
    Great news, the trackers we employed have found where
the noble and his warriors are. Turns out they've taken up
residence in an old fortified mansion somewhere east of 
__guild_. It's in the hills way off the beaten track, 
and miles away from anywhere.
<ce>
    They must have re-built the place somewhat, as by all 
accounts it's been rundown for several generations to the 
point where most local people are not even aware of its 
existence. 
<ce>
    The place is called __fort_ and I've enclosed
a map of how to reach it. Good luck and let me know if
you need any help once you uncover a bit more about the 
situation. Tread carefully, I doubt that they'll be very
forgiving if you anger them.
 
<ce>                           Yours sincerely,
 
<ce>                            _contact_

Message: 1067
What? A Dark Brotherhood contract on his daughter?
That's obcene! 
I will setup a meeting for you with the brotherhood,
as they should be willing to at least meet with a 
master of our guild out of respect. Whether they
will help you is another matter entirely.
Go to _dbTavern_ over in __dbTavern_, I'll
ensure that a representative will be waiting for 
you. Be careful around the dark brothers, they 
are not to be trifled with.

Message: 1068
I hope you're able to retrieve the contract and 
free _armorer_, %pcf. Remember, be careful around
the dark brothers, we want to keep on their good
side for the sake of the guild.

Message: 1069
Excellent work Master! So you have retrieved the
contract and now you can free _armorer_, %pcf.
<ce>
Bring him here, so we can help arrange a nice quiet 
place for him to live. I'm sure we can find somewhere
suitable.

-- Rescued armorer
Message: 1070
Welcome to __guild_, _armorer_. Please accept our help
after your ordeal.
<ce>
We will help him get back on his feet and set up a forge
in a small town somewhere nice and quiet. 
Where would you like to go _armorer_? We assume some
place far away from the ____guild_..

Message: 1071
I'm not sure. To tell you the truth I'm just grateful
to be out of the cursed place and out of 
_lord_'s clutches at last.
<ce>
I'm forever in your debt Master %pcn!
<ce>
Yes, I think that I will settle somewhere peaceful at
a new forge and finally take on an apprentice to pass
my skills on to before I get too old. I'm certainly tired 
of crafting armor myself after the past couple of years.
<ce>
I will always make an exception for you though %pcf.
I may not ever be able to repay you for rescuing me, 
however I can forge custom armor for you once I'm settled. 
Drop by anytime.
<ce>
So which direction shall I go? Hmmm, what do you think, 
Master %pcn?
<ce>
Shall I head West, South, or maybe to the warmer South West?

Message: 1072
So do you think I should head South to find 
a place to settle?

Message: 1073
What do you think about me heading West to find 
somewhere to settle?

Message: 1074
Okay then maybe I should head South-West and find
somewhere nice and warm to settle?

Message: 1075
Okay then %pcf, I'm going to head south and settle in
a small town called Pjiga in the Mournoth region. Please
drop by any time you're in the area or if you need some
particular armor making, you will always be welcome.
Goodbye and thanks again!

Message: 1076
Okay then %pcf, I'm going to head west and settle in
a small town called Penmore in Glenpoint. Please do 
drop by any time you're in the area or if you need some
particular armor making, you will always be welcome.

Message: 1077
Okay then %pcf, I'm going to head south west to the sun
and sand to settle in a small place called Paponirea in
Tigonus. Please drop by any time you're in the area or 
when you need some specifc armor making. You will always 
be welcome at my home.


-- Dark Brother contact
Message: 1080
Hello. blah blah

Message: 1081
You are a good friend of the brotherhood, so we'll deal 
with this renegade swiftly and deliver the contract 
directly to you within a few days.

Message: 1082
We'll have to deal with this renegade brother at some point
and the contract will be void, but we don't feel any urgency
regarding the situation. There are many more important
machinations and intrigues to deal with first. You will
find this brother at _dbHideout_ in __dbHideout_, you've
permission to go and deal with them. Be warned however, 
don't harm any others of our order while doing this, else 
we'll hunt you down and exact revenge. 
Given the circumstances we may make an exception for a
hefty price, but if you can't pay then you will die.

Message: 1090
An unsanctioned Dark Brotherhood contract to Kill 
_armorer_'s daughter if _lord_ reports that he's
fled __fort_!

Message: 1091
<ce> Reaching into your pack for something,
<ce> you spy a note that wasn't there before.
        <--->
<ce> You feel a parchment slip into your hand and hear
<ce> the sound of footsteps scampering away. There is 
<ce> no other sign of the mysterious courier.

QBN:

-- local guild hall
Place _guild_ local fighters
-- fortified mansion house
Place _fortExt_ permanent Northrock_Fort_Ext
Place _fort_ permanent Northrock_Fort
-- remote tavern & pawn shop
Place _dbTavern_ remote tavern
Place _dbHideout_ remote pawnshop

Person _contact_ faction The_Fighters_Guild
Person _armorer_ named Fred_Smith male

Person _lord_ named Lord_Verathon male
Person _captain_ named Captain_Ulthega male

Person _dbContact_ faction The_Dark_Brotherhood male
Foe _darkBrother_ is Assassin

Foe _doorGuard_ is Warrior
Foe _guard1_ is Warrior
Foe _guard2_ is Warrior
Foe _guard3_ is Warrior
Foe _guard4_ is Warrior
Foe _guard5_ is Warrior
Foe _guard6_ is Warrior
Foe _guard7_ is Warrior
Foe _guard8_ is Warrior
Foe _guard9_ is Warrior
Foe _guard10_ is Warrior

Foe _soldiers_ is 8 Warrior
Foe _soldiersChase_ is 2 Warrior

Item _letter1_ letter used 1066
Item _dbContract_ letter used 1090

Clock _trackers_ 00.02:00
Clock _dbDeal_ 00.02:00
Clock _killAll_ 02.00:00

-- rescues: 1Mill, kill 60 in 2 days, retrieve DB contract (steal, critKill, fight, highrep)
-- db approaches: stealth,pickpocket or crit,streetwise or repDB - mess up & fight & costs 250k gp & -rep
-- states: intro,ask,request,denied,permit

-- Quest start-up:
    place npc _contact_ at _guild_
    place npc _armorer_ at _fort_ marker 6
    place npc _lord_ at _fort_ marker 1
    place npc _captain_ at _fort_ marker 2
    place foe _doorGuard_ at _fort_ marker 3
    restrain foe _doorGuard_
    place foe _guard1_ at _fort_ marker 4
    place foe _guard2_ at _fort_ marker 5
    place foe _guard3_ at _fort_ marker 6
    place foe _guard4_ at _fort_ marker 7
    place foe _guard5_ at _fort_ marker 8
    place foe _guard6_ at _fort_ marker 9
    place foe _guard7_ at _fort_ marker 10
    place foe _guard8_ at _fort_ marker 11
    place foe _guard9_ at _fort_ marker 12
    place foe _guard10_ at _fort_ marker 13
    restrain foe _guard1_
    restrain foe _guard2_
    restrain foe _guard3_
    restrain foe _guard4_
    restrain foe _guard5_
    restrain foe _guard6_
    restrain foe _guard7_
    restrain foe _guard8_
    restrain foe _guard9_
    restrain foe _guard10_
    log 1010 step 0
    pc at _fortExt_ set _atFort_ saying 1026
    pc at _fort_ set _insideFort_ saying 1020
    

-- Guild contact speech to find the fort & meet DB
_contactFirst_ task:
    clicked npc _contact_ say 1060
    prompt 1061 yes _negotiateTrackers_ no _talkToWitnesses_

_talkToWitnesses_ task:
    say 1062

_negotiateTrackers_ task:
    say 1063

_employTrackers_ task:
    clicked _contact_ and at least 7000 gold otherwise do _talkToWitnesses_
    say 1064
    start timer _trackers_

_trackers_ task:
    reveal _fort_
    give pc _letter1_ notify 1065

_clickedContact_ task:
    clicked npc _contact_

_clearNpcClicks_ task:
    when _atFort_
    clear _clickedContact_
    clear _clickedArmorer_

_contactQuery_ task:
    when _clickedContact_ and _request_ and not _meetDB_ and not _rescued_
    say 1067
    log 1012 step 1
    setvar _meetDB_
    place npc _dbContact_ at _dbTavern_
    repute with _dbContact_ exceeds 30 do _dbRepHigh_  

_contactContract_ task:
    toting _dbContract_ and _contact_ clicked saying 1069
    unset _contactMeetDB_
    clear _clickedContact_

_contactMeetDB_ task:
    clicked npc _contact_ say 1068
    clear _clickedContact_

-- Successfully rescued, direction to settle?
_contactRescued_ task:
    when _clickedContact_ and _rescued_
    say 1070
    say 1071
    start task _dirSouth_

_dirSouth_ task:
    prompt 1072 yes _headS_ no _dirWest_

_dirWest_ task:
    prompt 1073 yes _headW_ no _dirSWest_

_dirSWest_ task:
    prompt 1074 yes _headSW_ no _dirRand_

_dirRand_ task:
    pick one of _headS_ _headW_ _headSW_

_headS_ task:
    worldupdate building ARMRAM03.RMB 14 at 240 in region 52 variant _master
    say 1075
    log 1015 step 3
    drop _armorer_ face
    give pc nothing
    end quest

_headW_ task:
    worldupdate building ARMRAM03.RMB 14 at 33 in region 18 variant _master
    say 1076
    log 1016 step 3
    drop _armorer_ face
    give pc nothing
    end quest

_headSW_ task:
    worldupdate building ARMRAM03.RMB 14 at 44 in region 48 variant _master
    say 1077
    log 1017 step 3
    drop _armorer_ face
    give pc nothing
    end quest


-- DB contact speech
_dbContactMeet_ task:
    clicked npc _dbContact_ 
    say 1080

_dbContactHiRep_ task:
    when _dbContactMeet_ and _dbRepHigh_
    say 1081
    log 1013 step 2
    start timer _dbDeal_

_dbContactLoRep_ task:
    when _dbContactMeet_ and not _dbRepHigh_
    say 1082
    log 1014 step 2
    place foe _darkBrother_ at _dbHideout_
    give item _dbContract_ to _darkBrother_

_dbDeal_ task:
    destroy _dbContact_
    give pc _dbContract_ notify 1091


variable _dbRepHigh_
variable _atFort_
variable _insideFort_
variable _hostile_
variable _intro_
variable _ask_
variable _request_
variable _meetDB_
variable _denied_
variable _permit_
variable _rescued_

_clickedLord_ task:
    clicked npc _lord_

_clickedCaptain_ task:
    clicked npc _captain_

_clickedArmorer_ task:
    clicked npc _armorer_

_attackedGuard_ task:
    injured _doorGuard_
    injured _guard1_
    injured _guard2_
    injured _guard3_
    injured _guard4_
    injured _guard5_
    injured _guard6_
    injured _guard7_
    injured _guard8_
    injured _guard9_
    injured _guard10_
    say 1023
    setvar _hostile_

_makeHostile_ task:
    when _hostile_ and _insideFort_
    start timer _killAll_
    enemies makehostile
    create foe _soldiers_ every 1 minutes 2 times with 100% success
    create foe _soldiers_ every 2 minutes indefinitely with 100% success

_chaseHostile_ task:
    when _hostile_ and not _insideFort_ and _atFort_
    say 1025
    create foe _soldiersChase_ every 1 minutes 4 times with 100% success

_submission_ task:
    killed 20 _soldiers_ saying 1022
    unset _clickedCaptain_
    unset _clickedLord_
    unset _lordPaid_
    unset _hostile_
    stop timer _killAll_
    enemies clear
    setvar _permit_

_killAll_ task:
    end quest saying 1003

-- Lord speech
_lordNoIntro_ task:
    when _clickedLord_ and not _intro_
    say 1030
    setvar _hostile_

_lordIntro_ task:
    when _clickedLord_ and _intro_ and not _ask_ and not _hostile_
    say 1031
    say 1032
    setvar _ask_
    clear _clickedLord_

_lordAsk_ task:
    when _clickedLord_ and _intro_ and _ask_ and not _request_
    say 1033
    clear _clickedLord_

-- request freedom
_lordRequest_ task:
    when _clickedLord_ and _request_ and not _permit_ and not _hostile_
    say 1034
    setvar _denied_

_lordGoAway_ task:
    say 1033

_lordForce_ task:
    toting _dbContract_ and _lord_ clicked saying 1036
    setvar _permit_
    clear _clickedLord_

- will need to allow LoCs here
_lordPaid_ task:
    clicked _lord_ and at least 70 gold otherwise do _lordGoAway_
    say 1035
    clear _clickedCaptain_
    clear _clickedLord_
    setvar _permit_

_lordPaidHostile_ task:
    when _permit_ and _hostile_
    stop timer _killAll_
    enemies clear
    unset _hostile_

_lordHavePermit_ task:
    when _clickedLord_ and _request_ and _permit_ and not _hostile_
    say 1037
    clear _clickedLord_


-- Captain speech:
_captainNoIntro_ task:
    when _clickedCaptain_ and not _intro_ and not _hostile_
    say 1040
    setvar _intro_
    clear _clickedCaptain_

_captainIntro_ task:
    when _clickedCaptain_ and _intro_ and not _ask_
    say 1041
    clear _clickedCaptain_

_captainAsk_ task:
    when _clickedCaptain_ and _intro_ and _ask_ and not _request_
    say 1042
    clear _clickedCaptain_

_captainHavePermit_ task:
    when _clickedCaptain_ and _permit_ and not _hostile_
    say 1043


-- Armorer speech
_armorerHostile_ task:
    when _clickedArmorer_ and _hostile_
    say 1058
    clear _clickedArmorer_

_armorerNoIntro_ task:
    when _clickedArmorer_ and not _intro_ and not _hostile_ and not _permit_
    say 1050
    clear _clickedArmorer_

_seenTrespassing_ task:
    when _armorerNoIntro_ and not _stealth_
    setvar _hostile_
    say 1021

_armorerIntro_ task:
    when _clickedArmorer_ and _intro_ and not _ask_
    say 1051
    clear _clickedArmorer_

_armorerAsk_ task:
    when _clickedArmorer_ and _intro_ and _ask_ and not _request_
    say 1052
    say 1053
    log 1011 step 1
    setvar _request_
    clear _clickedArmorer_

_rescue_ task:
    when _clickedArmorer_ and _permit_ 
    say 1059
    hide npc _armorer_ 
	add _armorer_ face
    setvar _rescued_
    clear _clickedContact_
    unset _contactMeetDB_
    unset _contactContract_

-- Skill checks
_stealth_ task:
    when skill Stealth is at least 60

_pickpocket_ task:
    when skill Pickpocket is at least 60

_streetwise_ task:
    when skill Streetwise is at least 60

_crit_ task:
    when skill CriticalStrike is at least 60

_backstab_ task:
    when skill Backstabbing is at least 60

_thief_ task:
    when _stealth_ and _pickpocket_

_backKill_ task:
    when _stealth_ and _backstab_

_quietKill_ task:
    when _streetwise_ and _crit_

